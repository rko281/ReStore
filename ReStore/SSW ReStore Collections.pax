| package |
package := Package name: 'SSW ReStore Collections'.
package paxVersion: 1;
	basicComment: 'ReStore
©2019 John Aspinall
https://github.com/rko281/ReStore'.


package classNames
	add: #SSWDBCacheDictionaryEntry;
	add: #SSWDBCacheDictionaryWrapper;
	add: #SSWDBCollectionAddition;
	add: #SSWDBCollectionChange;
	add: #SSWDBCollectionEntry;
	add: #SSWDBCollectionIndexUpdate;
	add: #SSWDBCollectionKeyUpdate;
	add: #SSWDBCollectionRemoval;
	add: #SSWDBCollectionRemovalWithKey;
	add: #SSWDBCollectionSpec;
	add: #SSWDBCollectionUpdate;
	add: #SSWDBGeneralCollectionSpec;
	add: #SSWDBKeyedCollectionEntry;
	add: #SSWDBKeyedStaticCollectionSpec;
	add: #SSWDBMultiValueCacheDictionaryEntry;
	add: #SSWDBMultiValueCacheDictionaryWrapper;
	add: #SSWDBOrderedStaticCollectionSpec;
	add: #SSWDBOwnedCacheDictionarySpec;
	add: #SSWDBOwnedCollectionSpec;
	add: #SSWDBOwnedMultiValueCacheDictionarySpec;
	add: #SSWDBSortedStaticCollectionSpec;
	add: #SSWDBStaticCacheDictionarySpec;
	add: #SSWDBStaticCollectionSpec;
	add: #SSWDBStaticDictionarySpec;
	add: #SSWDBStaticMultiValueCacheDictionarySpec;
	yourself.

package methodNames
	add: #ArrayedCollection -> #_changesFrom:;
	add: #ArrayedCollection -> #_dbAdd:suggestedIndex:;
	add: #Class -> #beKeyClassOf:;
	add: #Class -> #beTargetClassOf:;
	add: #Collection -> #_changesFrom:;
	add: #Collection -> #_collectionAdditions;
	add: #Collection -> #_dbAdd:;
	add: #Collection -> #_dbAdd:suggestedIndex:;
	add: #Collection -> #_dbKeyUpdateClass;
	add: #Collection -> #_dbRemovalClass;
	add: #Collection -> #_fetchAll:;
	add: #Collection -> #_keyedChangesFrom:;
	add: #Collection -> #_keyedCollectionAdditions;
	add: #Collection -> #_nonKeyedCollectionAdditions;
	add: #Collection -> #_proxifyIn:;
	add: #Collection -> #_valuesRemovedBy:;
	add: #Collection -> #fetch:;
	add: #Collection -> #fetchAll:;
	add: #Collection -> #isOrdered;
	add: #Collection -> #isSortedCollection;
	add: #Collection -> #isValidForPersistence;
	add: #Collection -> #of:;
	add: #Collection -> #of:owner:;
	add: #Collection -> #of:specClass:;
	add: #Collection -> #orRemote;
	add: #Collection -> #ownedCollectionSpecClass;
	add: #Collection -> #remote;
	add: #Collection -> #staticCollectionSpecClass;
	add: #Dictionary -> #_changesFrom:;
	add: #Dictionary -> #_collectionAdditions;
	add: #Dictionary -> #_dbAdd:suggestedIndex:;
	add: #Dictionary -> #_dbKeyUpdateClass;
	add: #Dictionary -> #_dbRemovalClass;
	add: #Dictionary -> #_isMultiValue;
	add: #Dictionary -> #_multiValueChangesFrom:;
	add: #Dictionary -> #_multiValueKeyedCollectionAdditions;
	add: #Dictionary -> #_valuesRemovedBy:;
	add: #Dictionary -> #of:;
	add: #Dictionary -> #of:keyClass:;
	add: #Dictionary -> #of:keyedOn:;
	add: #Dictionary -> #of:owner:;
	add: #Dictionary -> #of:owner:keyedOn:;
	add: #Dictionary -> #staticCollectionSpecClass;
	add: #OrderedCollection -> #_changesFrom:;
	add: #SequenceableCollection -> #_collectionAdditions;
	add: #SequenceableCollection -> #isOrdered;
	add: #SequenceableCollection -> #staticCollectionSpecClass;
	add: #Set -> #_changesFrom:;
	add: #Set -> #_superValuesRemovedBy:;
	add: #Set -> #_valuesRemovedBy:;
	add: #SortedCollection -> #_changesFrom:;
	add: #SortedCollection -> #_collectionAdditions;
	add: #SortedCollection -> #_dbAdd:;
	add: #SortedCollection -> #isSortedCollection;
	add: #SortedCollection -> #orderingSpecsFor:in:;
	add: #SortedCollection -> #staticCollectionSpecClass;
	add: #SSWDBDependentWrapper -> #beKeyClassOf:;
	add: #SSWDBDependentWrapper -> #beTargetClassOf:;
	add: #SSWDBRelatedWrapper -> #beKeyClassOf:;
	add: #SSWDBRelatedWrapper -> #beTargetClassOf:;
	add: #SSWDBUnrelatedWrapper -> #beKeyClassOf:;
	add: #SSWDBUnrelatedWrapper -> #beTargetClassOf:;
	add: 'Collection class' -> #of:;
	add: 'Collection class' -> #of:owner:;
	yourself.

package binaryGlobalNames: (Set new
	yourself).

package globalAliases: (Set new
	yourself).

package setPrerequisites: #(
	'..\Core\Object Arts\Dolphin\Base\Dolphin'
	'SSW ReStore Base Additions'
	'SSW ReStore SQL Entities'
	'SSW ReStore SQL Queries'
	'SSW ReStore Tables').

package!

"Class Definitions"!

Object subclass: #SSWDBCacheDictionaryWrapper
	instanceVariableNames: 'dictionary keyAccessor'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
Object subclass: #SSWDBCollectionChange
	instanceVariableNames: 'affectedEntry'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
Object subclass: #SSWDBCollectionEntry
	instanceVariableNames: 'table entryID sourceID target'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWDBCacheDictionaryWrapper subclass: #SSWDBMultiValueCacheDictionaryWrapper
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWDBCollectionChange subclass: #SSWDBCollectionAddition
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWDBCollectionChange subclass: #SSWDBCollectionIndexUpdate
	instanceVariableNames: 'newIndex'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWDBCollectionChange subclass: #SSWDBCollectionRemoval
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWDBCollectionChange subclass: #SSWDBCollectionUpdate
	instanceVariableNames: 'newTarget'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWDBCollectionIndexUpdate subclass: #SSWDBCollectionKeyUpdate
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWDBCollectionRemoval subclass: #SSWDBCollectionRemovalWithKey
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWDBCollectionEntry subclass: #SSWDBCacheDictionaryEntry
	instanceVariableNames: 'keyAccessor'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWDBCollectionEntry subclass: #SSWDBKeyedCollectionEntry
	instanceVariableNames: 'index'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWDBCacheDictionaryEntry subclass: #SSWDBMultiValueCacheDictionaryEntry
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWDBTableComponent subclass: #SSWDBCollectionSpec
	instanceVariableNames: 'templateCollection isValueDependent isValueRelated readStatement refreshStatement refreshWithContentsIfUpdatedStatement deleteStatement'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWDBCollectionSpec subclass: #SSWDBGeneralCollectionSpec
	instanceVariableNames: 'sourceField targetField collectionTable'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWDBCollectionSpec subclass: #SSWDBOwnedCollectionSpec
	instanceVariableNames: 'targetClass ownerField ownerAccessor orderingSpecs'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWDBGeneralCollectionSpec subclass: #SSWDBStaticCollectionSpec
	instanceVariableNames: 'targetClass deleteEntriesStatement'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWDBStaticCollectionSpec subclass: #SSWDBKeyedStaticCollectionSpec
	instanceVariableNames: 'indexField updateIndexStatement'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWDBStaticCollectionSpec subclass: #SSWDBSortedStaticCollectionSpec
	instanceVariableNames: 'orderingSpecs'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWDBStaticCollectionSpec subclass: #SSWDBStaticCacheDictionarySpec
	instanceVariableNames: 'keyAccessor'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWDBKeyedStaticCollectionSpec subclass: #SSWDBOrderedStaticCollectionSpec
	instanceVariableNames: 'updateTargetStatement'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWDBKeyedStaticCollectionSpec subclass: #SSWDBStaticDictionarySpec
	instanceVariableNames: 'keyClass isKeyDependent isKeyRelated keyTableForRead keyTableForRefresh deleteKeysStatement'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWDBStaticCacheDictionarySpec subclass: #SSWDBStaticMultiValueCacheDictionarySpec
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWDBOwnedCollectionSpec subclass: #SSWDBOwnedCacheDictionarySpec
	instanceVariableNames: 'keyAccessor'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWDBOwnedCacheDictionarySpec subclass: #SSWDBOwnedMultiValueCacheDictionarySpec
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!

"Global Aliases"!


"Loose Methods"!

!ArrayedCollection methodsFor!

_changesFrom: aCollectionOfEntries

	"Return the changes made to the receiver compared to aCollectionOf(Collection)Entries.
	For an array, there are no additions or removals - just updates"

	| entriesSize changes |

	entriesSize := aCollectionOfEntries size. 
	changes := OrderedCollection new: (self size // 10).

	self keysAndValuesDo:
		[ :index :each |
		index > entriesSize
		ifTrue:
			[each isNil ifFalse: 
				[changes add: (SSWDBCollectionAddition added: each at: index)]]
		ifFalse:
			[| entry |
			entry := aCollectionOfEntries at: index.
			entry target == each ifFalse: 
				[changes add: (SSWDBCollectionUpdate entry: entry newTarget: each)]]].

	^changes!

_dbAdd: anObject suggestedIndex: anInteger

	"Translate to at:put:"

	^self at: anInteger put: anObject

! !
!ArrayedCollection categoriesForMethods!
_changesFrom:!comparing!private! !
_dbAdd:suggestedIndex:!adding!private! !
!

!Class methodsFor!

beKeyClassOf: aCollectionSpec

	"The receiver has been specified as the class of objects which are the keys 
	of the collection specified by aCollectionSpec.
	Initialize aCollectionSpec accordingly"

	aCollectionSpec keyClass: self!

beTargetClassOf: aCollectionSpec

	"The receiver has been specified as the class of objects held in 
	the collection specified by aCollectionSpec.
	Initialize aCollectionSpec accordingly"

	aCollectionSpec targetClass: self! !
!Class categoriesForMethods!
beKeyClassOf:!accessing!public! !
beTargetClassOf:!accessing!public! !
!

!Collection methodsFor!

_changesFrom: aCollectionOfEntries

	"Return the changes made to the receiver compared to aCollectionOf(SSWDBCollection)Entries,
	as a collection of SSWDBCollectionChanges"

	^self subclassResponsibility!

_collectionAdditions

	"Return an collection of SSWDBCollectionAdditions representing the addition of 
	all the receiver's elements to an empty collection.
	Default - defer to _nonKeyedCollectionAdditions (assume receiver does not support keys). 
	Collections with keys/indexes should reimplement to include key/index information"

	^self _nonKeyedCollectionAdditions!

_dbAdd: anObject

	"For use when instantiating a collection from the database.
	Normally the same as add: but see SortedCollection implementation"

	^self add: anObject!

_dbAdd: anObject suggestedIndex: anInteger

	"Add anObject to the receiver, possibly at the index anInteger.
	The suggestedIndex is only required by keyed collections so defer to just _dbAdd:"

	^self _dbAdd: anObject!

_dbKeyUpdateClass

	^SSWDBCollectionIndexUpdate!

_dbRemovalClass

	^SSWDBCollectionRemoval!

_fetchAll: aCollection

	"Private - ReStore - for each element of the receiver, use a single query (ideally, or multiple batched queries if the receiver's size is too large for the readAll batch size)
	to fetch from the database the components in the inst vars named in aCollection"

	| any reStore table |

	self isEmpty ifTrue: [^self].
	any := self anyOne. 
	reStore := any _reStore.
	table := reStore tableForClass: any class.

	aCollection do: [ :instVar | (table componentAccessing: instVar) readAllFor: self]

	!

_keyedChangesFrom: aCollectionOfEntries

	"Return the changes made to the receiver compared to aCollectionOf(Collection)Entries.
	For keyed changes, must consider additions, removals and changes of key"

	| changes map |

	changes := OrderedCollection new: (self size // 10).
	map := IdentityDictionary new: aCollectionOfEntries size.

	"Make a map of each element (as was) to its entry(s)"
	aCollectionOfEntries do: 
		[ :each | (map at: each target ifAbsentPut: [OrderedCollection new: 1]) addLast: each].

	"Locate and remove entry for each member of the receiver and check for key change"
	self keysAndValuesDo:
		[ :newKey :each || entry |
		entry := (map at: each ifAbsent: [##(OrderedCollection new)]) removeFirstIfAbsent:
			"No entry found = element has been added to the receiver" 
			[(changes add: (SSWDBCollectionAddition added: each at: newKey)) affectedEntry].
		entry index = newKey ifFalse: 
			[changes add: (self _dbKeyUpdateClass entry: entry newIndex: newKey)]].

	"Any remaining entries indicate elements removed from the receiver"
	map keysAndValuesDo:
		[ :each :entries | 
		entries isEmpty ifFalse:		"Minor optimisation"
			[entries do: [ :entry | changes add: (self _dbRemovalClass newWithEntry: entry)]]].

	^changes!

_keyedCollectionAdditions

	"Return a collection of SSWDBCollectionAdditions representing the addition of 
	all the receiver's elements to an empty collection. 
	Include the key of each element (as used by keysAndValuesDo:)"

	| additions |

	additions := OrderedCollection new.

	self keysAndValuesDo: [ :key :each | additions add: (SSWDBCollectionAddition added: each at: key)].

	^additions!

_nonKeyedCollectionAdditions

	"Return an collection of SSWDBCollectionAdditions representing the addition of 
	all the receiver's elements to an empty collection.
	The SSWDBCollectionAdditions do not contain key information"

	| additions |

	additions := OrderedCollection new.

	self do: [ :each | additions add: (SSWDBCollectionAddition added: each)].

	^additions!

_proxifyIn: aCollectionProxy

	aCollectionProxy _proxiedObject: self.
	^self!

_valuesRemovedBy: aCollectionOfChanges

	"Return those values (objects) completely removed from the receiver by aCollectionOfChanges"

	| affectedObjects |

	affectedObjects := aCollectionOfChanges collect: [ :change | change affectedEntry target].

	^affectedObjects asIdentitySet reject: [ :object | self identityIncludes: object]!

fetch: aSymbol

	"ReStore - for each element of the receiver, use a single query (ideally, or multiple batched queries if the receiver's size is too large for the readAll batch size)
	to fetch from the database the component in the inst var named aSymbol"

	^self fetchAll: (Array with: aSymbol)!

fetchAll: aCollection

	"ReStore - for each element of the receiver, use a single query (ideally, or multiple batched queries if the receiver's size is too large for the readAll batch size)
	to fetch from the database the components in the inst vars named in aCollection"

	^(self select: [ :each | each _isRecovered]) _fetchAll: aCollection!

isOrdered

	^false
!

isSortedCollection

	^false
!

isValidForPersistence

	"Collections are not persisted directly (e.g. via storeIn:), therefore consider them already persistent"

	^true!

of: targetClass

	"ReStore specification method - specifies a general (M to M) collection containing aClass.
	Create and return an appropriate SSWDBCollectionSpec"

	^self of: targetClass specClass: self staticCollectionSpecClass!

of: targetClass owner: aSymbol

	"ReStore specification method - specifies a 1 to many collection, containing aClass,
	whose inst var named aSymbol holds the owning (1) object.

	Create and return an appropriate SSWDBCollectionSpec"

	^self ownedCollectionSpecClass new
		templateCollection: self;
		setTargetClass: targetClass;
		owner: aSymbol;
		yourself
!

of: targetClass specClass: specClass

	"ReStore specification method - specifies a general (M to M) collection containing aClass.
	Create, initialize and return a configured instance of the given SSWDBCollectionSpec class"

	^specClass new
		templateCollection: self;
		setTargetClass: targetClass;
		yourself!

orRemote

	"The receiver is already in memory, return it"

	^self!

ownedCollectionSpecClass 

	"Return the class of owned collection spec that is appropriate for this kind of collection"

	^SSWDBOwnedCollectionSpec

!

remote

	"Defer to the proxy"

	^self _dbProxy remote!

staticCollectionSpecClass 

	"Return the class of static collection spec that is appropriate for this kind of collection.
	Subclasses for which this may differ depending on mode of use should not implement this,
	but should route configuration through of:specClass: "

	^SSWDBStaticCollectionSpec

! !
!Collection categoriesForMethods!
_changesFrom:!private!restore-internal! !
_collectionAdditions!private!restore-internal! !
_dbAdd:!private!restore-internal! !
_dbAdd:suggestedIndex:!adding!private! !
_dbKeyUpdateClass!private!restore-internal! !
_dbRemovalClass!private!restore-internal! !
_fetchAll:!private!restore-internal! !
_keyedChangesFrom:!private!restore-internal! !
_keyedCollectionAdditions!private!restore-internal! !
_nonKeyedCollectionAdditions!private!restore-internal! !
_proxifyIn:!private!restore-internal! !
_valuesRemovedBy:!private!restore-internal! !
fetch:!converting!public! !
fetchAll:!converting!public! !
isOrdered!public!testing! !
isSortedCollection!public!testing! !
isValidForPersistence!comparing!private! !
of:!public!specification! !
of:owner:!public!specification! !
of:specClass:!public!specification! !
orRemote!converting!public! !
ownedCollectionSpecClass!constants!private! !
remote!converting!public! !
staticCollectionSpecClass!constants!private! !
!

!Collection class methodsFor!

of: targetClass

	^self new of: targetClass
!

of: targetClass owner: aSymbol

	^self new of: targetClass owner: aSymbol

! !
!Collection class categoriesForMethods!
of:!public!specification! !
of:owner:!public!specification! !
!

!Dictionary methodsFor!

_changesFrom: aCollectionOfEntries

	"Override to include key information if necessary. 
	This can be determined from any member of aCollectionOfEntries.
	Defer to _collectionAdditions if empty (to give keyed entries, issue#0026) "

	^aCollectionOfEntries isEmpty 
	ifTrue:
		[self _collectionAdditions]
	ifFalse:
		[(aCollectionOfEntries first isKindOf: SSWDBCacheDictionaryEntry)
		ifTrue: 
			[self _isMultiValue
				ifTrue: [self _multiValueChangesFrom: aCollectionOfEntries]
				ifFalse: [super _changesFrom: aCollectionOfEntries]]
		ifFalse: 
			[self _keyedChangesFrom: aCollectionOfEntries]]!

_collectionAdditions

	"Override to 
	 - check for multi-value dictionary
	 - include key information"

	^self _isMultiValue
		ifTrue: [self _multiValueKeyedCollectionAdditions]
		ifFalse: [self _keyedCollectionAdditions]!

_dbAdd: targetObject suggestedIndex: keyObject

	"Make the receiver polymorphic with an indexed collection. keyObject = index"

	^self at: keyObject put: targetObject!

_dbKeyUpdateClass

	^SSWDBCollectionKeyUpdate!

_dbRemovalClass

	^SSWDBCollectionRemovalWithKey!

_isMultiValue

	^self isEmpty not and: [(self anyOne isKindOf: Collection) and: [self anyOne isString not]]!

_multiValueChangesFrom: aCollectionOfEntries

	"Return the changes made to the receiver compared to aCollectionOf(Dictionary)Entries.
	The receiver is a multi-value dictionary"

	| allValues changes |

	allValues := self inject: (IdentitySet new: self size * 4) into: [ :all :values | all addAll: values; yourself].
	changes := OrderedCollection new.

	aCollectionOfEntries do: 
		[ :entry |
		((self at: entry key ifAbsent: [#()]) includes: entry target) 
			ifTrue: [allValues remove: entry target]
			ifFalse: [changes add: (SSWDBCollectionRemoval newWithEntry: entry)]].

	allValues do: [ :each | changes add: (SSWDBCollectionAddition added: each)].

	^changes!

_multiValueKeyedCollectionAdditions

	| additions |

	additions := OrderedCollection new.

	self keysAndValuesDo: [ :key :values | values do: [ :each | additions add: (SSWDBCollectionAddition added: each at: key)]].

	^additions!

_valuesRemovedBy: aCollectionOfChanges

	"The receiver is not a set (i.e. allows duplicates) and so cannot take advantage of superclass optimisation"

	^self _superValuesRemovedBy: aCollectionOfChanges!

of: anAssociation

	"ReStore specification method - specify a Dictionary which stores instances of the class which is the value of anAssociation.
	The key of Association should be either:
	 - the class of the keys of the Dictionary, e.g.  Dictionary of: DateAndTime -> CustomerOrder
	OR
	 - an instance variable name (Symbol) of the value class; the contents of this instance variable is used as the key for each element, e.g. Dictionary of: #surname -> Person
	OR
	 - a 1-arg Block which is evaluated with each elements to derive the key, e.g. Dictionary of: [ :obj | obj name asLowercase] -> Person
	Create and return an appropriate SSWDBCollectionSpec"

	^(anAssociation key isSymbol or: [anAssociation key isKindOf: BlockClosure])
		ifTrue: [self of: anAssociation value keyedOn: anAssociation key]
		ifFalse: [self of: anAssociation value keyClass: anAssociation key]
!

of: targetClass keyClass: keyClass

	"Private - ReStore specification method - specify a Dictionary which stores instances of targetClass.
	The Dictionary's keys will be of the class keyClass, which should be a valid persistent class.
	Create and return an appropriate SSWDBCollectionSpec"

	^(self of: targetClass specClass: SSWDBStaticDictionarySpec)
		setKeyClass: keyClass;
		yourself!

of: targetClass keyedOn: aSymbolOrBlock

	"Private - ReStore specification method - specify a Dictionary which stores instances of targetClass.
	The target class may alternatively be a collection specification (e.g. OrderedCollection of: Person) for a multi-value dictionary.
	Instances are stored in the receiver at: the value detemined by evaluating aSymbolOrBlock with them (usually but not always the value of an instance variable).
	Create and return an appropriate SSWDBCollectionSpec"

	| spec |

	(targetClass isKindOf: SSWDBCollectionSpec)
	ifTrue: 
		[spec := self of: targetClass targetClass specClass: SSWDBStaticMultiValueCacheDictionarySpec.
		targetClass isValueDependent ifTrue: [spec beValueDependent]]
	ifFalse: 
		[spec := self of: targetClass specClass: SSWDBStaticCacheDictionarySpec].

	^spec
		keyedOn: aSymbolOrBlock;
		yourself!

of: anAssociation owner: aSymbol

	"Private - ReStore specification method - specify a Dictionary which stores instances of the class which is the value of anAssociation.
	The key of Association should be an instance variable name (Symbol) of the value class; the contents of this instance variable is used as the key for each element, e.g. Dictionary of: #surname -> Person
	Create and return an appropriate SSWDBCollectionSpec"

	^self of: anAssociation value owner: aSymbol keyedOn: anAssociation key!

of: targetClass owner: ownerSymbol keyedOn: aSymbol

	"Private - ReStore specification method - specify a Dictionary which stores instances of targetClass.
	The target class may alternatively be a collection specification (e.g. OrderedCollection of: Person) for a multi-value dictionary.
	Instances are stored in the receiver at: the value detemined by evaluating aSymbolOrBlock with them (usually but not always the value of an instance variable).
	Create and return an appropriate SSWDBCollectionSpec"

	| spec |

	(targetClass isKindOf: SSWDBCollectionSpec)
	ifTrue: 
		[spec := self of: targetClass targetClass specClass: SSWDBOwnedMultiValueCacheDictionarySpec.
		targetClass isValueDependent ifTrue: [spec beValueDependent]]
	ifFalse: 
		[spec := self of: targetClass specClass: SSWDBOwnedCacheDictionarySpec].

	^spec
		owner: ownerSymbol;
		keyedOn: aSymbol;
		yourself!

staticCollectionSpecClass 

	"Do not implement this method, the staticCollectionSpecClass for Dictionaries varies
	(see of:xxxx: variants)"

	^self shouldNotImplement

! !
!Dictionary categoriesForMethods!
_changesFrom:!comparing!private! !
_collectionAdditions!comparing!private! !
_dbAdd:suggestedIndex:!adding!private! !
_dbKeyUpdateClass!private!restore-internal! !
_dbRemovalClass!private!restore-internal! !
_isMultiValue!private!testing! !
_multiValueChangesFrom:!comparing!private! !
_multiValueKeyedCollectionAdditions!comparing!private! !
_valuesRemovedBy:!comparing!private! !
of:!public!specification! !
of:keyClass:!private!specification! !
of:keyedOn:!private!specification! !
of:owner:!private!specification! !
of:owner:keyedOn:!private!specification! !
staticCollectionSpecClass!constants!private! !
!

!OrderedCollection methodsFor!

_changesFrom: aCollectionOfEntries

	"Return the changes made to the receiver compared to aCollectionOf(Collection)Entries.
	For an OrderedCollection, must include full keyed (indexed) changes"

	^self _keyedChangesFrom: aCollectionOfEntries! !
!OrderedCollection categoriesForMethods!
_changesFrom:!comparing!private! !
!

!SequenceableCollection methodsFor!

_collectionAdditions

	"Reimplemented to include index (key) information"

	^self _keyedCollectionAdditions!

isOrdered

	^true
!

staticCollectionSpecClass 

	^SSWDBOrderedStaticCollectionSpec

! !
!SequenceableCollection categoriesForMethods!
_collectionAdditions!comparing!private! !
isOrdered!public!testing! !
staticCollectionSpecClass!constants!private! !
!

!Set methodsFor!

_changesFrom: aSetOfEntries

	"Return the changes made to the receiver compared to aSetOf(Collection)Entries.
	For a Set, consider additions and removals only"

	| changes oldSet |

	changes := OrderedCollection new.
	oldSet := aSetOfEntries collect: [ :entry | entry target].

	self do: [ :each | (oldSet identityIncludes: each) ifFalse: 
						[changes add: (SSWDBCollectionAddition added: each)]].

	aSetOfEntries do: [ :entry | (self identityIncludes: entry target) ifFalse: 
						[changes add: (SSWDBCollectionRemoval newWithEntry: entry)]].

	^changes!

_superValuesRemovedBy: aCollectionOfChanges

	"Pass-thru to default implementation of _valuesRemovedBy: for benefit of non-Set subclasses"

	^super _valuesRemovedBy: aCollectionOfChanges
!

_valuesRemovedBy: aCollectionOfChanges

	"Overriden to take advantage of the lack of duplicates in the receiver:
	if a change removes an element from the receiver, then all occurrences of that element are removed"

	^aCollectionOfChanges collect: [ :each | each affectedEntry target]! !
!Set categoriesForMethods!
_changesFrom:!comparing!private! !
_superValuesRemovedBy:!comparing!private! !
_valuesRemovedBy:!comparing!private! !
!

!SortedCollection methodsFor!

_changesFrom: aCollectionOfEntries

	"Return the changes made to the receiver compared to aCollectionOf(Collection)Entries.
	For a SortedCollection, can ignore index changes"

	| changes map |

	changes := OrderedCollection new: (self size // 10).
	map := IdentityDictionary new: aCollectionOfEntries size.

	"Make a map of each element (as was) to its entry(s)"
	aCollectionOfEntries do: 
		[ :each | (map at: each target ifAbsentPut: [OrderedCollection new: 1]) addLast: each].

	"Locate and remove entry for each member of the receiver"
	self keysAndValuesDo:
		[ :newIndex :each |
		(map at: each ifAbsent: [##(OrderedCollection new)]) removeFirstIfAbsent:
			"No entry found = element has been added to the receiver" 
			[changes add: (SSWDBCollectionAddition added: each)]].

	"Any remaining entries indicate elements removed from the receiver"
	map keysAndValuesDo:
		[ :each :entries | 
		entries isEmpty ifFalse:		"Minor optimisation"
			[entries do: [ :entry | changes add: (SSWDBCollectionRemoval newWithEntry: entry)]]].

	^changes!

_collectionAdditions

	"Defer to _nonKeyedCollectionAdditions, since indexing does not need to be stored 
	(it is defined by the sorting criteria)"

	^self _nonKeyedCollectionAdditions!

_dbAdd: anObject

	"For use when instantiating a collection from the database.
	Sorting is done by the recovery query, so we need to use an unconditional addLast: to avoice the expense of sorting, and potentially interrogating a non-recovered persistent object"

	^super addLast: anObject!

isSortedCollection

	^true
!

orderingSpecsFor: aClass in: aReStore

	"Convert the receiver's sortBlock into DBOrderingSpecs"

	^(self sortBlock = SortedCollection new sortBlock and: [aClass isPersistentBaseClass]) "default ascending sort"
	ifTrue:
		[OrderedCollection with: (SSWDBOrderingSpec ascending: #target)]
	ifFalse:
		[| instances |
		instances := aReStore instancesOf: aClass.
		instances sortBlock: (self sortBlock ifNil: [SortedCollection defaultSortBlock]).
		instances parser orderingSpecs collect: 
			[ :each || field |
			field := each criteria.
			field isNil 
				ifTrue: [each criteria: #target]
				ifFalse: [each criteria: field accessor name]]]!

staticCollectionSpecClass 

	^SSWDBSortedStaticCollectionSpec

! !
!SortedCollection categoriesForMethods!
_changesFrom:!comparing!private! !
_collectionAdditions!comparing!private! !
_dbAdd:!comparing!private! !
isSortedCollection!public!testing! !
orderingSpecsFor:in:!accessing!private! !
staticCollectionSpecClass!constants!private! !
!

!SSWDBDependentWrapper methodsFor!

beKeyClassOf: aTableComponent

	"Polymorphic with the Class implementation"
		
	super beKeyClassOf: aTableComponent.
	aTableComponent beKeyDependent!

beTargetClassOf: aTableComponent

	"Polymorphic with the Class implementation"

	super beTargetClassOf: aTableComponent.
	aTableComponent beValueDependent! !
!SSWDBDependentWrapper categoriesForMethods!
beKeyClassOf:!defining!public! !
beTargetClassOf:!defining!public! !
!

!SSWDBRelatedWrapper methodsFor!

beKeyClassOf: aTableComponent

	"Polymorphic with the Class implementation"

	self relatedObject beKeyClassOf: aTableComponent.
	aTableComponent beKeyRelated!

beTargetClassOf: aTableComponent

	"Polymorphic with the Class implementation"

	self relatedObject beTargetClassOf: aTableComponent.
	aTableComponent beValueRelated! !
!SSWDBRelatedWrapper categoriesForMethods!
beKeyClassOf:!defining!public! !
beTargetClassOf:!defining!public! !
!

!SSWDBUnrelatedWrapper methodsFor!

beKeyClassOf: aTableComponent

	"Polymorphic with the Class implementation"

	self unrelatedObject beKeyClassOf: aTableComponent.
	aTableComponent beKeyUnrelated!

beTargetClassOf: aTableComponent

	"Polymorphic with the Class implementation"

	self unrelatedObject beTargetClassOf: aTableComponent.
	aTableComponent beValueUnrelated! !
!SSWDBUnrelatedWrapper categoriesForMethods!
beKeyClassOf:!defining!public! !
beTargetClassOf:!defining!public! !
!

"End of package definition"!

