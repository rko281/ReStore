| package |
package := Package name: 'SSW ReStore SQL Entities'.
package paxVersion: 1;
	basicComment: 'ReStore
©2019 John Aspinall
https://github.com/rko281/ReStore'.


package classNames
	add: #SSWMSAccess2013Dialect;
	add: #SSWMSAccessDialect;
	add: #SSWMySQLDialect;
	add: #SSWPostgreSQLDialect;
	add: #SSWSQLCollection;
	add: #SSWSQLCondition;
	add: #SSWSQLConditionCollection;
	add: #SSWSQLDialect;
	add: #SSWSQLField;
	add: #SSWSQLFieldAssignment;
	add: #SSWSQLFieldAssignmentCollection;
	add: #SSWSQLFieldCollection;
	add: #SSWSQLFieldValueConjunction;
	add: #SSWSQLFunction;
	add: #SSWSQLFunctionField;
	add: #SSWSQLiteDialect;
	add: #SSWSQLJoinedTableCollection;
	add: #SSWSQLLeftJoinedTableCollection;
	add: #SSWSQLOperator;
	add: #SSWSQLParameterField;
	add: #SSWSQLQueryParameter;
	add: #SSWSQLServerDialect;
	add: #SSWSQLTable;
	add: #SSWSQLTableAllFields;
	add: #SSWSQLTableCollection;
	add: #SSWSQLTableComponent;
	add: #SSWSQLWriteStream;
	yourself.

package methodNames
	add: #Boolean -> #then:else:;
	add: #ByteArray -> #writeSQLOn:;
	add: #ByteArray -> #writeSQLOn:forField:;
	add: #Character -> #reStoreValue;
	add: #Collection -> #writeSQLOn:;
	add: #Collection -> #writeSQLOn:forField:;
	add: #Collection -> #writeSQLOn:with:;
	add: #Date -> #asSQLStringIn:;
	add: #DateAndTime -> #asSQLStringIn:;
	add: #False -> #asSQLStringIn:;
	add: #Object -> #ifNull:;
	add: #Object -> #ifNull:ifNotNull:;
	add: #Object -> #isSQLNull;
	add: #Object -> #sqlCanBeParameterized;
	add: #Object -> #sqlStringIn:;
	add: #Object -> #writeSQLOn:forCondition:;
	add: #Object -> #writeSQLOn:forField:;
	add: #SSWDBByteArrayWithInfo -> #writeSQLTypeOn:using:;
	add: #SSWDBDateAndTimeWithResolution -> #writeSQLTypeOn:using:;
	add: #SSWDBFixedStringWithInfo -> #writeSQLTypeOn:using:;
	add: #SSWDBScaledDecimalWithInfo -> #writeSQLTypeOn:using:;
	add: #SSWDBVariableStringWithInfo -> #writeSQLTypeOn:using:;
	add: #String -> #asSQLFunction;
	add: #String -> #asSQLFunctionBooleanResult;
	add: #String -> #asSQLFunctionIntegerResult;
	add: #String -> #asSQLFunctionStringResult;
	add: #String -> #asSQLFunctionWithResultClass:;
	add: #String -> #writeSQLOn:;
	add: #String -> #writeSQLOn:forField:;
	add: #Time -> #asSQLStringIn:;
	add: #True -> #asSQLStringIn:;
	add: #UndefinedObject -> #isSQLNull;
	add: #UndefinedObject -> #writeSQLOn:;
	add: #UndefinedObject -> #writeSQLOn:forCondition:;
	add: 'Boolean class' -> #writeSQLTypeOn:using:;
	add: 'ByteArray class' -> #writeSQLTypeOn:using:;
	add: 'Date class' -> #writeSQLTypeOn:using:;
	add: 'DateAndTime class' -> #writeSQLTypeOn:using:;
	add: 'Float class' -> #writeSQLTypeOn:using:;
	add: 'Integer class' -> #writeSQLTypeOn:using:;
	add: 'LargeInteger class' -> #writeSQLTypeOn:using:;
	add: 'Metaclass class' -> #writeSQLTypeOn:using:;
	add: 'Number class' -> #writeSQLTypeOn:using:;
	add: 'Object class' -> #writeSQLTypeOn:using:;
	add: 'String class' -> #writeSQLTypeOn:using:;
	add: 'Time class' -> #writeSQLTypeOn:using:;
	yourself.

package binaryGlobalNames: (Set new
	yourself).

package globalAliases: (Set new
	yourself).

package setPrerequisites: #(
	'..\Core\Object Arts\Dolphin\Base\Dolphin'
	'..\Core\Object Arts\Dolphin\Base\Dolphin Legacy Date & Time'
	'SSW ReStore Base Additions').

package!

"Class Definitions"!

Object subclass: #SSWSQLDialect
	instanceVariableNames: 'connection classNameTransformation instVarNameTransformation maxColumnNameLength maxTableNameLength sqlFunctions maxColumnBytes unsizedStringSize unsizedBlobSize standardNamespace'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
Object subclass: #SSWSQLFieldValueConjunction
	instanceVariableNames: 'field value'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
Object subclass: #SSWSQLFunction
	instanceVariableNames: 'parameterizedString resultParamIndex staticResultClass isAggregateFunction'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
Object subclass: #SSWSQLOperator
	instanceVariableNames: 'sqlString messageSelector isInverted'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: 'messageSQLMap messageInverseSQLMap'!
Object subclass: #SSWSQLParameterField
	instanceVariableNames: 'value'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
Object subclass: #SSWSQLQueryParameter
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
Object subclass: #SSWSQLTable
	instanceVariableNames: 'name'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
Object subclass: #SSWSQLTableComponent
	instanceVariableNames: 'table'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
OrderedCollection variableSubclass: #SSWSQLCollection
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWSQLCollection variableSubclass: #SSWSQLConditionCollection
	instanceVariableNames: 'sqlConcatenator'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWSQLCollection variableSubclass: #SSWSQLFieldAssignmentCollection
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWSQLCollection variableSubclass: #SSWSQLFieldCollection
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWSQLCollection variableSubclass: #SSWSQLTableCollection
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWSQLTableCollection variableSubclass: #SSWSQLJoinedTableCollection
	instanceVariableNames: 'joinConditions'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWSQLJoinedTableCollection variableSubclass: #SSWSQLLeftJoinedTableCollection
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWSQLDialect subclass: #SSWMSAccessDialect
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWSQLDialect subclass: #SSWMySQLDialect
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWSQLDialect subclass: #SSWPostgreSQLDialect
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWSQLDialect subclass: #SSWSQLiteDialect
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWSQLDialect subclass: #SSWSQLServerDialect
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWMSAccessDialect subclass: #SSWMSAccess2013Dialect
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWSQLFieldValueConjunction subclass: #SSWSQLCondition
	instanceVariableNames: 'operator'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWSQLFieldValueConjunction subclass: #SSWSQLFieldAssignment
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWSQLTableComponent subclass: #SSWSQLField
	instanceVariableNames: 'name'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWSQLTableComponent subclass: #SSWSQLTableAllFields
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
SSWSQLField subclass: #SSWSQLFunctionField
	instanceVariableNames: 'function arguments'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
WriteStream subclass: #SSWSQLWriteStream
	instanceVariableNames: 'dialect fieldAliases tableAliases parameterizedFields parameterizedValues placeholderCount'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!

"Global Aliases"!


"Loose Methods"!

!Boolean methodsFor!

then: trueValue else: otherValue

	"SQL control flow message; maps to a CASE statement.
	Really just here to prevent unimplemented selector warnings but can be used where image/database agnostic code is needed"

	^self ifTrue: [trueValue] ifFalse: [otherValue]! !
!Boolean categoriesForMethods!
then:else:!control flow!public! !
!

!Boolean class methodsFor!

writeSQLTypeOn: aStream using: anSQLDialect

	aStream nextPutAll: anSQLDialect boolean! !
!Boolean class categoriesForMethods!
writeSQLTypeOn:using:!printing!public! !
!

!ByteArray methodsFor!

writeSQLOn: aStream

	"Avoid SQL injection and other issues by always using parameterized values for ByeArrays"
	self writeSQLOn: aStream forField: (SSWSQLParameterField forValue: self)!

writeSQLOn: aStream forField: aField

	"Opt out of the Collection implementation"
	^aStream nextPut: self forField: aField! !
!ByteArray categoriesForMethods!
writeSQLOn:!printing!public! !
writeSQLOn:forField:!printing!public! !
!

!ByteArray class methodsFor!

writeSQLTypeOn: aStream using: aSQLDialect

	aStream nextPutAll: aSQLDialect blob! !
!ByteArray class categoriesForMethods!
writeSQLTypeOn:using:!printing!public! !
!

!Character methodsFor!

reStoreValue

	"As far as SQL is concerned, a Character == a String"
	
	^String with: self! !
!Character categoriesForMethods!
reStoreValue!accessing!public! !
!

!Collection methodsFor!

writeSQLOn: aStream

	self writeSQLOn: aStream with: [ :object | object writeSQLOn: aStream]!

writeSQLOn: aStream forField: aField

	self writeSQLOn: aStream with: [ :object | object writeSQLOn: aStream forField: aField]!

writeSQLOn: aStream with: aBlock

	"Private - Collections are comma-separated and parentheses-wrapped in SQL"
	
	aStream nextPut: $(.

	self 
		do: aBlock
		separatedBy: [aStream nextPut: $,].
	
	aStream nextPut: $)! !
!Collection categoriesForMethods!
writeSQLOn:!printing!public! !
writeSQLOn:forField:!printing!public! !
writeSQLOn:with:!printing!private! !
!

!Date methodsFor!

asSQLStringIn: aSQLDialect

	^aSQLDialect dateStringFor: self! !
!Date categoriesForMethods!
asSQLStringIn:!converting!public! !
!

!Date class methodsFor!

writeSQLTypeOn: aStream using: anSQLDialect

	^aStream nextPutAll: anSQLDialect date! !
!Date class categoriesForMethods!
writeSQLTypeOn:using:!printing!public! !
!

!DateAndTime methodsFor!

asSQLStringIn: aSQLDialect

	^aSQLDialect dateAndTimeStringFor: self! !
!DateAndTime categoriesForMethods!
asSQLStringIn:!converting!public! !
!

!DateAndTime class methodsFor!

writeSQLTypeOn: aStream using: anSQLDialect

	aStream nextPutAll: anSQLDialect datetime! !
!DateAndTime class categoriesForMethods!
writeSQLTypeOn:using:!printing!public! !
!

!False methodsFor!

asSQLStringIn: aSQLDialect

	"ASSERT: falseObject is a String in this dialect"
	^aSQLDialect falseObject! !
!False categoriesForMethods!
asSQLStringIn:!converting!public! !
!

!Float class methodsFor!

writeSQLTypeOn: aStream  using: anSQLDialect

	aStream nextPutAll: anSQLDialect double! !
!Float class categoriesForMethods!
writeSQLTypeOn:using:!printing!public! !
!

!Integer class methodsFor!

writeSQLTypeOn: aStream  using: anSQLDialect

	"Use INT - long integer"
	
	aStream nextPutAll: anSQLDialect integer! !
!Integer class categoriesForMethods!
writeSQLTypeOn:using:!printing!public! !
!

!LargeInteger class methodsFor!

writeSQLTypeOn: aStream  using: anSQLDialect

	"Map LargeInteger to (typically) BIGINT"
	
	aStream nextPutAll: anSQLDialect largeInteger! !
!LargeInteger class categoriesForMethods!
writeSQLTypeOn:using:!printing!public! !
!

!Metaclass class methodsFor!

writeSQLTypeOn: aStream using: anSQLDialect

	"Classes are stored as their name.
	Use a VARCHAR at 128 characters"
	
	(String maxSize: 128) writeSQLTypeOn: aStream using: anSQLDialect! !
!Metaclass class categoriesForMethods!
writeSQLTypeOn:using:!printing!public! !
!

!Number class methodsFor!

writeSQLTypeOn: aStream  using: anSQLDialect

	"Default to single float"
	
	aStream nextPutAll: anSQLDialect float! !
!Number class categoriesForMethods!
writeSQLTypeOn:using:!printing!public! !
!

!Object methodsFor!

ifNull: aValue

	"SQL control flow message; maps to IFNULL().
	Really just here to prevent unimplemented selector warnings but can be used where image/database agnostic code is needed"

	^self ifNil: [aValue]!

ifNull: aValue ifNotNull: otherValue

	"SQL control flow message; maps to a CASE statement.
	Really just here to prevent unimplemented selector warnings but can be used where image/database agnostic code is needed"

	^self ifNil: [aValue] ifNotNil: [otherValue]!

isSQLNull

	^false!

sqlCanBeParameterized

	"Can the SQL representation of the receiver be parameterized?"
	^true!

sqlStringIn: anSSWSQLDialect

	| stream |
	
	stream := anSSWSQLDialect sqlWriteStream.
	
	self writeSQLOn: stream.
	
	^stream contents!

writeSQLOn: aStream forCondition: aSQLCondition

	^self writeSQLOn: aStream forField: aSQLCondition field!

writeSQLOn: aStream forField: aField

	^aStream nextPut: self forField: aField! !
!Object categoriesForMethods!
ifNull:!control flow!public! !
ifNull:ifNotNull:!control flow!public! !
isSQLNull!public!testing! !
sqlCanBeParameterized!public!testing! !
sqlStringIn:!displaying!public! !
writeSQLOn:forCondition:!printing!public! !
writeSQLOn:forField:!printing!public! !
!

!Object class methodsFor!

writeSQLTypeOn: aStream using: anSQLDialect

	"If not overriden, assume a persistent class with a unique ID"
	
	self reStoreIDClass writeSQLTypeOn: aStream using: anSQLDialect! !
!Object class categoriesForMethods!
writeSQLTypeOn:using:!printing!public! !
!

!SSWDBByteArrayWithInfo methodsFor!

writeSQLTypeOn: aStream using: anSQLDialect

	aStream nextPutAll: ((anSQLDialect blobTypeForSize: self fieldSize) expandMacrosWith: self fieldSize)! !
!SSWDBByteArrayWithInfo categoriesForMethods!
writeSQLTypeOn:using:!printing!public! !
!

!SSWDBDateAndTimeWithResolution methodsFor!

writeSQLTypeOn: aStream  using: anSQLDialect

	aStream nextPutAll: (anSQLDialect datetimeWithResolution expandMacrosWith: self resolution)! !
!SSWDBDateAndTimeWithResolution categoriesForMethods!
writeSQLTypeOn:using:!printing!public! !
!

!SSWDBFixedStringWithInfo methodsFor!

writeSQLTypeOn: aStream using: anSQLDialect

	"If the receiver's size exceeds the maximum allowed, use a longText (if available)"

	(self fieldSize > anSQLDialect maxChar)
		ifTrue: [aStream nextPutAll: (anSQLDialect unsizedStringTypeForSize: self fieldSize)]
		ifFalse: [aStream nextPutAll: (anSQLDialect char expandMacrosWith: self fieldSize)]! !
!SSWDBFixedStringWithInfo categoriesForMethods!
writeSQLTypeOn:using:!printing!public! !
!

!SSWDBScaledDecimalWithInfo methodsFor!

writeSQLTypeOn: aStream  using: anSQLDialect

	"Form is something like DECIMAL(precision,scale)"

	| actualPrecision |

	self precision isNil 
		ifTrue: [actualPrecision := 15]
		ifFalse: [actualPrecision := self precision].

	aStream nextPutAll: (anSQLDialect decimal expandMacrosWith: actualPrecision with: self scale)! !
!SSWDBScaledDecimalWithInfo categoriesForMethods!
writeSQLTypeOn:using:!printing!public! !
!

!SSWDBVariableStringWithInfo methodsFor!

writeSQLTypeOn: aStream using: anSQLDialect

	"If the receiver's size exceeds the maximum allowed, use a longText (if available)"

	(self fieldSize > anSQLDialect maxVarchar)
		ifTrue: [aStream nextPutAll: (anSQLDialect unsizedStringTypeForSize: self fieldSize)]
		ifFalse: [aStream nextPutAll: (anSQLDialect varchar expandMacrosWith: self fieldSize)]! !
!SSWDBVariableStringWithInfo categoriesForMethods!
writeSQLTypeOn:using:!printing!public! !
!

!String methodsFor!

asSQLFunction

	^SSWSQLFunction new
		parameterizedString: self;
		yourself!

asSQLFunctionBooleanResult

	^self asSQLFunctionWithResultClass: Integer!

asSQLFunctionIntegerResult

	^self asSQLFunctionWithResultClass: Integer!

asSQLFunctionStringResult

	^self asSQLFunctionWithResultClass: String!

asSQLFunctionWithResultClass: aClass

	^self asSQLFunction
		staticResultClass: aClass;
		yourself!

writeSQLOn: aStream

	"Avoid SQL injection and other issues by always using parameterized values for Strings"
	self writeSQLOn: aStream forField: (SSWSQLParameterField forValue: self)!

writeSQLOn: aStream forField: aField

	"Opt out of the Collection implementation"
	^aStream nextPut: self forField: aField! !
!String categoriesForMethods!
asSQLFunction!converting!public! !
asSQLFunctionBooleanResult!converting!public! !
asSQLFunctionIntegerResult!converting!public! !
asSQLFunctionStringResult!converting!public! !
asSQLFunctionWithResultClass:!converting!public! !
writeSQLOn:!printing!public! !
writeSQLOn:forField:!printing!public! !
!

!String class methodsFor!

writeSQLTypeOn: aStream using: anSQLDialect

	"As this is the non-sized variant, use a reasonably-sized variable text type"
	
	aStream nextPutAll: anSQLDialect unsizedString! !
!String class categoriesForMethods!
writeSQLTypeOn:using:!printing!public! !
!

!Time methodsFor!

asSQLStringIn: aSQLDialect

	^aSQLDialect timeStringFor: self! !
!Time categoriesForMethods!
asSQLStringIn:!converting!public! !
!

!Time class methodsFor!

writeSQLTypeOn: aStream using: anSQLDialect

	aStream nextPutAll: anSQLDialect time! !
!Time class categoriesForMethods!
writeSQLTypeOn:using:!printing!public! !
!

!True methodsFor!

asSQLStringIn: aSQLDialect

	"ASSERT: trueObject is a String in this dialect"
	^aSQLDialect trueObject! !
!True categoriesForMethods!
asSQLStringIn:!converting!public! !
!

!UndefinedObject methodsFor!

isSQLNull

	^true!

writeSQLOn: aStream

	aStream nextPutAll: 'NULL'!

writeSQLOn: aStream forCondition: aSQLCondition

	"Do not parameterize - NULL not allowed as parameterized condition value in some DBs"
	self writeSQLOn: aStream! !
!UndefinedObject categoriesForMethods!
isSQLNull!public!testing! !
writeSQLOn:!printing!public! !
writeSQLOn:forCondition:!printing!public! !
!

"End of package definition"!

